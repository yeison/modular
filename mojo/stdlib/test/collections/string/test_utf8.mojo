# ===----------------------------------------------------------------------=== #
# Copyright (c) 2025, Modular Inc. All rights reserved.
#
# Licensed under the Apache License v2.0 with LLVM Exceptions:
# https://llvm.org/LICENSE.txt
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
# ===----------------------------------------------------------------------=== #
# RUN: %mojo %s

from collections.string._utf8 import (
    _count_utf8_continuation_bytes,
    _is_valid_utf8,
)
from sys.info import alignof, sizeof

from memory import Span, UnsafePointer
from testing import assert_equal, assert_false, assert_raises, assert_true

# ===----------------------------------------------------------------------=== #
# Reusable testing data
# ===----------------------------------------------------------------------=== #

alias GOOD_SEQUENCES = [
    List("a".as_bytes()),
    List("\xc3\xb1".as_bytes()),
    List("\xe2\x82\xa1".as_bytes()),
    List("\xf0\x90\x8c\xbc".as_bytes()),
    List("ì•ˆë…•í•˜ì„¸ìš”, ì„¸ìƒ".as_bytes()),
    List("\xc2\x80".as_bytes()),
    List("\xf0\x90\x80\x80".as_bytes()),
    List("\xee\x80\x80".as_bytes()),
    List("very very very long string ðŸ”¥ðŸ”¥ðŸ”¥".as_bytes()),
]


alias BAD_SEQUENCES = [
    List[Byte](0xC3, 0x28),  # continuation bytes does not start with 10xx
    List[Byte](0xA0, 0xA1),  # first byte is continuation byte
    List[Byte](0xE2, 0x28, 0xA1),  # second byte should be continuation byte
    List[Byte](0xE2, 0x82, 0x28),  # third byte should be continuation byte
    List[Byte](
        0xF0, 0x28, 0x8C, 0xBC
    ),  # second byte should be continuation byte
    List[Byte](
        0xF0, 0x90, 0x28, 0xBC
    ),  # third byte should be continuation byte
    List[Byte](
        0xF0, 0x28, 0x8C, 0x28
    ),  # fourth byte should be continuation byte
    List[Byte](0xC0, 0x9F),  # overlong, could be just one byte
    List[Byte](0xF5, 0xFF, 0xFF, 0xFF),  # missing continuation bytes
    List[Byte](0xED, 0xA0, 0x81),  # UTF-16 surrogate pair
    List[Byte](0xF8, 0x90, 0x80, 0x80, 0x80),  # 5 bytes is too long
    List("123456789012345".as_bytes())
    + List[Byte](0xED),  # Continuation bytes are missing
    List("123456789012345".as_bytes())
    + List[Byte](0xF1),  # Continuation bytes are missing
    List("123456789012345".as_bytes())
    + List[Byte](0xC2),  # Continuation bytes are missing
    List[Byte](0xC2, 0x7F),  # second byte is not continuation byte
    List[Byte](0xCE),  # Continuation byte missing
    List[Byte](0xCE, 0xBA, 0xE1),  # two continuation bytes missing
    List[Byte](0xCE, 0xBA, 0xE1, 0xBD),  # One continuation byte missing
    List[Byte](
        0xCE, 0xBA, 0xE1, 0xBD, 0xB9, 0xCF
    ),  # fifth byte should be continuation byte
    List[Byte](
        0xCE, 0xBA, 0xE1, 0xBD, 0xB9, 0xCF, 0x83, 0xCE
    ),  # missing continuation byte
    List[Byte](
        0xCE, 0xBA, 0xE1, 0xBD, 0xB9, 0xCF, 0x83, 0xCE, 0xBC, 0xCE
    ),  # missing continuation byte
    List[Byte](0xDF),  # missing continuation byte
    List[Byte](0xEF, 0xBF),  # missing continuation byte
]

# ===----------------------------------------------------------------------=== #
# Tests
# ===----------------------------------------------------------------------=== #


fn test_utf8_validation() raises:
    var text = """Lorem ipsum dolor sit amet, consectetur adipiscing elit. Nam
    varius tellus quis tincidunt dictum. Donec eros orci, ultricies ac metus non
    , rutrum faucibus neque. Nunc ultricies turpis ut lacus consequat dapibus.
    Nulla nec risus a purus volutpat blandit. Donec sit amet massa velit. Aenean
    fermentum libero eu pharetra placerat. Sed id molestie tellus. Fusce
    sollicitudin a purus ac placerat.
    Lorem Ipsumï¼Œä¹Ÿç§°ä¹±æ•°å‡æ–‡æˆ–è€…å“‘å…ƒæ–‡æœ¬ï¼Œ æ˜¯å°åˆ·åŠæŽ’ç‰ˆé¢†åŸŸæ‰€å¸¸ç”¨çš„è™šæ‹Ÿæ–‡å­—
    ç”±äºŽæ›¾ç»ä¸€å°åŒ¿åçš„æ‰“å°æœºåˆ»æ„æ‰“ä¹±äº†ä¸€ç›’å°åˆ·å­—ä½“ä»Žè€Œé€ å‡ºä¸€æœ¬å­—ä½“æ ·å“ä¹¦ï¼ŒLorem
    Ipsumä»Žè¥¿å…ƒ15ä¸–çºªèµ·å°±è¢«ä½œä¸ºæ­¤é¢†åŸŸçš„æ ‡å‡†æ–‡æœ¬ä½¿ç”¨ã€‚å®ƒä¸ä»…å»¶ç»­äº†äº”ä¸ªä¸–çºªï¼Œ
    è¿˜é€šè¿‡äº†ç”µå­æŽ’ç‰ˆçš„æŒ‘æˆ˜ï¼Œå…¶é›å½¢å´ä¾ç„¶ä¿å­˜è‡³ä»Šã€‚åœ¨1960å¹´ä»£ï¼Œâ€Leatrasetâ€å…¬å¸å‘å¸ƒäº†å°åˆ·ç€
    Lorem Ipsumæ®µè½çš„çº¸å¼ ï¼Œä»Žè€Œå¹¿æ³›æ™®åŠäº†å®ƒçš„ä½¿ç”¨ã€‚æœ€è¿‘ï¼Œè®¡ç®—æœºæ¡Œé¢å‡ºç‰ˆè½¯ä»¶
    ×œ×ž×” ×× ×• ×ž×©×ª×ž×©×™× ×‘×–×”?
    ×–×•×”×™ ×¢×•×‘×“×” ×ž×‘×•×¡×¡×ª ×©×“×¢×ª×• ×©×œ ×”×§×•×¨× ×ª×”×™×” ×ž×•×¡×—×ª ×¢×œ ×™×“×™ ×˜×§×˜×¡ ×§×¨×™× ×›××©×¨ ×”×•× ×™×‘×™×˜ ×‘×¤×¨×™×¡×ª×•. ×”×ž×˜×¨×” ×‘×©×™×ž×•×©
     ×‘- Lorem Ipsum ×”×•× ×©×™×© ×œ×• ×¤×—×•×ª ××• ×™×•×ª×¨ ×ª×¤×•×¦×” ×©×œ ××•×ª×™×•×ª, ×‘× ×™×’×•×“ ×œ×ž×œ×œ ' ×™×¡×•×™
    ×™×¡×•×™  ×™×¡×•×™', ×•× ×•×ª×Ÿ ×—×–×•×ª ×§×¨×™××” ×™×•×ª×¨.×”×¨×‘×” ×”×•×¦××•×ª ×ž×—×©×‘×™× ×•×¢×•×¨×›×™ ×“×¤×™ ××™× ×˜×¨× ×˜ ×ž×©×ª×ž×©×™× ×›×™×•× ×‘-
    Lorem Ipsum ×›×˜×§×¡×˜ ×‘×¨×™×¨×ª ×”×ž×—×“×œ ×©×œ×”×, ×•×—×™×¤×•×© ×©×œ 'lorem ipsum' ×™×—×©×•×£ ××ª×¨×™× ×¨×‘×™× ×‘×¨××©×™×ª
    ×“×¨×›×.×’×¨×¡××•×ª ×¨×‘×•×ª × ×•×¦×¨×• ×‘×ž×”×œ×š ×”×©× ×™×, ×œ×¢×ª×™× ×‘×©×’×’×”
    Lorem Ipsum Ðµ ÐµÐ´Ð½Ð¾ÑÑ‚Ð°Ð²ÐµÐ½ Ð¼Ð¾Ð´ÐµÐ» Ð½Ð° Ñ‚ÐµÐºÑÑ‚ ÐºÐ¾Ñ˜ ÑÐµ ÐºÐ¾Ñ€Ð¸ÑÑ‚ÐµÐ» Ð²Ð¾ Ð¿ÐµÑ‡Ð°Ñ‚Ð°Ñ€ÑÐºÐ°Ñ‚Ð°
    Ð¸Ð½Ð´ÑƒÑÑ‚Ñ€Ð¸Ñ˜Ð°.
    Lorem Ipsum - Ñ†Ðµ Ñ‚ÐµÐºÑÑ‚-"Ñ€Ð¸Ð±Ð°", Ñ‰Ð¾ Ð²Ð¸ÐºÐ¾Ñ€Ð¸ÑÑ‚Ð¾Ð²ÑƒÑ”Ñ‚ÑŒÑÑ Ð² Ð´Ñ€ÑƒÐºÐ°Ñ€ÑÑ‚Ð²Ñ– Ñ‚Ð° Ð´Ð¸Ð·Ð°Ð¹Ð½Ñ–.
    Lorem Ipsum à¸„à¸·à¸­ à¹€à¸™à¸·à¹‰à¸­à¸«à¸²à¸ˆà¸³à¸¥à¸­à¸‡à¹à¸šà¸šà¹€à¸£à¸µà¸¢à¸šà¹† à¸—à¸µà¹ˆà¹ƒà¸Šà¹‰à¸à¸±à¸™à¹ƒà¸™à¸˜à¸¸à¸£à¸à¸´à¸ˆà¸‡à¸²à¸™à¸žà¸´à¸¡à¸žà¹Œà¸«à¸£à¸·à¸­à¸‡à¸²à¸™à¹€à¸£à¸µà¸¢à¸‡à¸žà¸´à¸¡à¸žà¹Œ
    à¸¡à¸±à¸™à¹„à¸”à¹‰à¸à¸¥à¸²à¸¢à¸¡à¸²à¹€à¸›à¹‡à¸™à¹€à¸™à¸·à¹‰à¸­à¸«à¸²à¸ˆà¸³à¸¥à¸­à¸‡à¸¡à¸²à¸•à¸£à¸à¸²à¸™à¸‚à¸­à¸‡à¸˜à¸¸à¸£à¸à¸´à¸ˆà¸”à¸±à¸‡à¸à¸¥à¹ˆà¸²à¸§à¸¡à¸²à¸•à¸±à¹‰à¸‡à¹à¸•à¹ˆà¸¨à¸•à¸§à¸£à¸£à¸©à¸—à¸µà¹ˆ
    Lorem ipsum" ÙÙŠ Ø£ÙŠ Ù…Ø­Ø±Ùƒ Ø¨Ø­Ø« Ø³ØªØ¸Ù‡Ø± Ø§Ù„Ø¹Ø¯ÙŠØ¯
     Ù…Ù† Ø§Ù„Ù…ÙˆØ§Ù‚Ø¹ Ø§Ù„Ø­Ø¯ÙŠØ«Ø© Ø§Ù„Ø¹Ù‡Ø¯ ÙÙŠ Ù†ØªØ§Ø¦Ø¬ Ø§Ù„Ø¨Ø­Ø«. Ø¹Ù„Ù‰ Ù…Ø¯Ù‰ Ø§Ù„Ø³Ù†ÙŠÙ†
     Ø¸Ù‡Ø±Øª Ù†Ø³Ø® Ø¬Ø¯ÙŠØ¯Ø© ÙˆÙ…Ø®ØªÙ„ÙØ© Ù…Ù† Ù†Øµ Ù„ÙˆØ±ÙŠÙ… Ø¥ÙŠØ¨Ø³ÙˆÙ…ØŒ Ø£Ø­ÙŠØ§Ù†Ø§Ù‹ Ø¹Ù† Ø·Ø±ÙŠÙ‚
     Ø§Ù„ØµØ¯ÙØ©ØŒ ÙˆØ£Ø­ÙŠØ§Ù†Ø§Ù‹ Ø¹Ù† Ø¹Ù…Ø¯ ÙƒØ¥Ø¯Ø®Ø§Ù„ Ø¨Ø¹Ø¶ Ø§Ù„Ø¹Ø¨Ø§Ø±Ø§Øª Ø§Ù„ÙÙƒØ§Ù‡ÙŠØ© Ø¥Ù„ÙŠÙ‡Ø§.
    """
    assert_true(_is_valid_utf8(text.as_bytes()))
    assert_true(_is_valid_utf8(text.as_bytes()))

    var positive = List[List[UInt8]](
        List[UInt8](0x0),
        List[UInt8](0x00),
        List[UInt8](0x66),
        List[UInt8](0x7F),
        List[UInt8](0x00, 0x7F),
        List[UInt8](0x7F, 0x00),
        List[UInt8](0xC2, 0x80),
        List[UInt8](0xDF, 0xBF),
        List[UInt8](0xE0, 0xA0, 0x80),
        List[UInt8](0xE0, 0xA0, 0xBF),
        List[UInt8](0xED, 0x9F, 0x80),
        List[UInt8](0xEF, 0x80, 0xBF),
        List[UInt8](0xF0, 0x90, 0xBF, 0x80),
        List[UInt8](0xF2, 0x81, 0xBE, 0x99),
        List[UInt8](0xF4, 0x8F, 0x88, 0xAA),
    )
    for item in positive:
        assert_true(_is_valid_utf8(Span(item[])))
        assert_true(_is_valid_utf8(Span(item[])))
    var negative = List[List[UInt8]](
        List[UInt8](0x80),
        List[UInt8](0xBF),
        List[UInt8](0xC0, 0x80),
        List[UInt8](0xC1, 0x00),
        List[UInt8](0xC2, 0x7F),
        List[UInt8](0xDF, 0xC0),
        List[UInt8](0xE0, 0x9F, 0x80),
        List[UInt8](0xE0, 0xC2, 0x80),
        List[UInt8](0xED, 0xA0, 0x80),
        List[UInt8](0xED, 0x7F, 0x80),
        List[UInt8](0xEF, 0x80, 0x00),
        List[UInt8](0xF0, 0x8F, 0x80, 0x80),
        List[UInt8](0xF0, 0xEE, 0x80, 0x80),
        List[UInt8](0xF2, 0x90, 0x91, 0x7F),
        List[UInt8](0xF4, 0x90, 0x88, 0xAA),
        List[UInt8](0xF4, 0x00, 0xBF, 0xBF),
        List[UInt8](
            0xC2, 0x80, 0x00, 0x00, 0xE1, 0x80, 0x80, 0x00, 0xC2, 0xC2, 0x80
        ),
        List[UInt8](0x00, 0xC2, 0xC2, 0x80, 0x00, 0x00, 0xE1, 0x80, 0x80),
        List[UInt8](0x00, 0x00, 0x00, 0xF1, 0x80, 0x00),
        List[UInt8](0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0xF1),
        List[UInt8](0x00, 0x00, 0x00, 0x00, 0xF1, 0x00, 0x80, 0x80),
        List[UInt8](0x00, 0x00, 0xF1, 0x80, 0xC2, 0x80, 0x00),
        List[UInt8](0x00, 0x00, 0xF0, 0x80, 0x80, 0x80),
    )
    for item in negative:
        assert_false(_is_valid_utf8(Span(item[])))
        assert_false(_is_valid_utf8(Span(item[])))


fn validate_utf8(span: Span[Byte]) -> Bool:
    return _is_valid_utf8(span)


def test_good_utf8_sequences():
    for sequence in GOOD_SEQUENCES:
        assert_true(validate_utf8(sequence[]))


def test_bad_utf8_sequences():
    for sequence in BAD_SEQUENCES:
        assert_false(validate_utf8(Span(sequence[])))


def test_stringslice_from_utf8():
    for sequence in GOOD_SEQUENCES:
        _ = StringSlice.from_utf8(Span(sequence[]))

    for sequence in BAD_SEQUENCES:
        with assert_raises(contains="buffer is not valid UTF-8"):
            _ = StringSlice.from_utf8(Span(sequence[]))


def test_combination_good_utf8_sequences():
    # any combination of good sequences should be good
    for i in range(0, len(GOOD_SEQUENCES)):
        for j in range(i, len(GOOD_SEQUENCES)):
            var sequence = GOOD_SEQUENCES[i] + GOOD_SEQUENCES[j]
            assert_true(validate_utf8(Span(sequence)))


def test_combination_bad_utf8_sequences():
    # any combination of bad sequences should be bad
    for i in range(0, len(BAD_SEQUENCES)):
        for j in range(i, len(BAD_SEQUENCES)):
            var sequence = BAD_SEQUENCES[i] + BAD_SEQUENCES[j]
            assert_false(validate_utf8(Span(sequence)))


def test_combination_good_bad_utf8_sequences():
    # any combination of good and bad sequences should be bad
    for i in range(0, len(GOOD_SEQUENCES)):
        for j in range(0, len(BAD_SEQUENCES)):
            var sequence = GOOD_SEQUENCES[i] + BAD_SEQUENCES[j]
            assert_false(validate_utf8(Span(sequence)))


def test_combination_10_good_utf8_sequences():
    # any 10 combination of good sequences should be good
    for i in range(0, len(GOOD_SEQUENCES)):
        for j in range(i, len(GOOD_SEQUENCES)):
            var sequence = GOOD_SEQUENCES[i] * 10 + GOOD_SEQUENCES[j] * 10
            assert_true(validate_utf8(Span(sequence)))


def test_combination_10_good_10_bad_utf8_sequences():
    # any 10 combination of good and bad sequences should be bad
    for i in range(0, len(GOOD_SEQUENCES)):
        for j in range(0, len(BAD_SEQUENCES)):
            var sequence = GOOD_SEQUENCES[i] * 10 + BAD_SEQUENCES[j] * 10
            assert_false(validate_utf8(Span(sequence)))


def test_count_utf8_continuation_bytes():
    alias c = UInt8(0b1000_0000)
    alias b1 = UInt8(0b0100_0000)
    alias b2 = UInt8(0b1100_0000)
    alias b3 = UInt8(0b1110_0000)
    alias b4 = UInt8(0b1111_0000)

    def _test(amnt: Int, items: List[UInt8]):
        var p = items.unsafe_ptr()
        var span = Span[Byte, StaticConstantOrigin](ptr=p, length=len(items))
        var str_slice = StringSlice(unsafe_from_utf8=span)
        assert_equal(amnt, _count_utf8_continuation_bytes(str_slice))

    _test(5, List[UInt8](c, c, c, c, c))
    _test(2, List[UInt8](b2, c, b2, c, b1))
    _test(2, List[UInt8](b2, c, b1, b2, c))
    _test(2, List[UInt8](b2, c, b2, c, b1))
    _test(2, List[UInt8](b2, c, b1, b2, c))
    _test(2, List[UInt8](b1, b2, c, b2, c))
    _test(2, List[UInt8](b3, c, c, b1, b1))
    _test(2, List[UInt8](b1, b1, b3, c, c))
    _test(2, List[UInt8](b1, b3, c, c, b1))
    _test(3, List[UInt8](b1, b4, c, c, c))
    _test(3, List[UInt8](b4, c, c, c, b1))
    _test(3, List[UInt8](b3, c, c, b2, c))
    _test(3, List[UInt8](b2, c, b3, c, c))


def main():
    test_utf8_validation()
    test_good_utf8_sequences()
    test_bad_utf8_sequences()
    test_stringslice_from_utf8()
    test_combination_good_utf8_sequences()
    test_combination_bad_utf8_sequences()
    test_combination_good_bad_utf8_sequences()
    test_combination_10_good_utf8_sequences()
    test_combination_10_good_10_bad_utf8_sequences()
    test_count_utf8_continuation_bytes()
