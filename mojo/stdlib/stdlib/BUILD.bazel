load("//bazel:api.bzl", "mojo_doc", "mojo_library", "pkg_filegroup", "pkg_files")

package(default_visibility = ["//visibility:public"])

exports_files(["__init__.mojo"])

filegroup(
    name = "stdlib_srcs",
    srcs = glob(["**/*.mojo"]),
)

mojo_library(
    name = "stdlib",
    srcs = [
        ":stdlib_srcs",
    ],
    build_docs = False,
)

LIBS = [
    "algorithm",
    "base64",
    "benchmark",
    "bit",
    "buffer",
    "builtin",
    "collections",
    "compile",
    "complex",
    "gpu",
    "hashlib",
    "json",
    "logger",
    "math",
    "memory",
    "os",
    "pathlib",
    "pwd",
    "python",
    "random",
    "regex",
    "runtime",
    "stat",
    "subprocess",
    "sys",
    "tempfile",
    "testing",
    "time",
    "utils",
]

[
    mojo_doc(
        name = lib + ".docs",
        srcs = glob([lib + "/**/*.mojo"]),
        # Building the docs for the standard library causes some issues with mojo doc.
        # It lets the local files take priority of the existing stdlib.mojopkg
        # resulting in broken lookups if you only have some of the files.
        data = glob(
            ["**/*.mojo"],
            exclude = [lib + "/**/*.mojo"],
        ),
        validate_missing_docs = True,
    )
    for lib in LIBS
]

[
    pkg_files(
        name = lib + "-doc-files",
        srcs = [":" + lib + ".docs"],
        renames = {
            lib + ".docs": lib,
        },
        visibility = ["//visibility:private"],
    )
    for lib in LIBS
]

pkg_filegroup(
    name = "docs",
    srcs = [
        ":" + lib + "-doc-files"
        for lib in LIBS
    ],
)
